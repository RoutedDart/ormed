set shell := ["bash", "-lc"]

MARIADB_COMPOSE := "docker-compose.yml"
MYSQL_COMPOSE := "docker-compose.mysql.yml"
MARIADB_URL := "mariadb://root:secret@localhost:6604/orm_test"
MYSQL_URL := "mysql://root:secret@localhost:6605/orm_test"

# Run both MariaDB and MySQL driver suites.
test:
	just test-mariadb
	just test-mysql

# Run the MariaDB-backed tests using the local docker-compose file.
test-mariadb:
	set -euo pipefail; \
	docker compose -p ormed-mariadb -f {{MARIADB_COMPOSE}} up -d --wait --wait-timeout 120; \
	trap 'docker compose -p ormed-mariadb -f {{MARIADB_COMPOSE}} down -v' EXIT; \
	sleep 2; \
	MARIADB_URL={{MARIADB_URL}} dart test test/mariadb_driver_shared_test.dart -r expanded

# Run the MySQL-backed tests using the local docker-compose.mysql file.
test-mysql:
	set -euo pipefail; \
	docker compose -p ormed-mysql -f {{MYSQL_COMPOSE}} up -d --wait --wait-timeout 120; \
	trap 'docker compose -p ormed-mysql -f {{MYSQL_COMPOSE}} down -v' EXIT; \
	sleep 2; \
	MYSQL_URL={{MYSQL_URL}} dart test test/mysql_driver_shared_test.dart -r expanded

# Stop any leftover containers.
down:
	docker compose -p ormed-mariadb -f {{MARIADB_COMPOSE}} down -v
	docker compose -p ormed-mysql -f {{MYSQL_COMPOSE}} down -v

up:
    docker compose -p ormed-mariadb -f {{MARIADB_COMPOSE}} up -d --wait --wait-timeout 120
    docker compose -p ormed-mysql -f {{MYSQL_COMPOSE}} up -d --wait --wait-timeout 120
